apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'shadow'

archivesBaseName = 'trumpet-server'
mainClassName = 'com.jayway.trumpet.server.boot.Main'

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

dependencies {

    compile 'org.eclipse.jetty:jetty-server:9.2.2.v20140723'
    compile 'org.eclipse.jetty:jetty-webapp:9.2.2.v20140723'
    //compile 'org.eclipse.jetty.aggregate:jetty-all:9.2.2.v20140723'

    compile 'org.glassfish.jersey.core:jersey-server:2.12'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.12'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.12'
    compile 'org.glassfish.jersey.media:jersey-media-sse:2.12'
    compile 'org.glassfish.jersey.ext:jersey-bean-validation:2.12'

    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'ch.qos.logback:logback-core:1.1.2'
    compile 'ch.qos.logback:logback-classic:1.1.2'

    compile 'org.aeonbits.owner:owner:1.0.5.1'

    compile 'com.google.guava:guava:18.0'

    compile 'com.ganyo:gcm-server:1.0.2'

    testCompile 'junit:junit:4.11'
    testCompile 'com.jayway.awaitility:awaitility:1.6.1'
    testCompile 'org.assertj:assertj-core:1.6.1'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.glassfish.jersey.core:jersey-client:2.12'
    testCompile 'com.jayway.jsonpath:json-path:0.9.1'
}

//
// Configure integration tests
//
configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}
check.dependsOn(integTest)